import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'de.undercouch.download' version '3.3.0'
}

ext {
    SQLITE_VERSION = '3210000'
    SQLITE_YEAR = '2017'
    SQLITE_BUNDLE_NAME = "sqlite-src-${SQLITE_VERSION}.zip"

    PUBLISH_GROUP_ID = 'com.plangrid.android'
    PUBLISH_ARTIFACT_ID = 'sqlite-fts5-android'
    PUBLISH_VERSION = '3.21.0'
}

apply plugin: 'com.android.library'
apply plugin: 'de.undercouch.download'

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments "-DSQLITE_ZIP:String=${buildDir}/${SQLITE_BUNDLE_NAME}"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
    buildToolsVersion '26.0.2'
}

dependencies {
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'io.requery:sqlite-android:3.21.0'
}

task downloadSqliteSource(type: Download) {
    src "http://www.sqlite.org/${SQLITE_YEAR}/${SQLITE_BUNDLE_NAME}"
    overwrite false
    dest buildDir
}

afterEvaluate {
    externalNativeBuildDebug.dependsOn downloadSqliteSource
    externalNativeBuildRelease.dependsOn downloadSqliteSource
}

apply from: 'https://raw.githubusercontent.com/blundell/release-android-library/master/android-release-aar.gradle'
